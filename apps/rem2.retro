( ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ )
( Retro Editor for Mortals ["rem"]                          )
( ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ )
( Distributed under the Creative Commons Zero License:      )
(                                                           )
( The person who associated a work with this document has   )
( dedicated this work to the Commons by waiving all of his  )
( or her rights to the work under copyright law and all     )
( related or neighboring legal rights he or she had in the  )
( work, to the extent allowable by law.                     )
(                                                           )
( Other Rights â€” In no way are any of the following rights  )
( affected by CC0:                                          )
(  * Patent or trademark rights held by the person who      )
(    associated this document with a work.                  )
(  * Rights other persons may have either in the work       )
(    itself or in how the work is used, such as publicity   )
(    or privacy rights.                                     )
(                                                           )
( Charles Childers, December 2009                           )
( Luke Parrish, December 2009                               )
( ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ )

variable rem:l  ( line   )
variable rem:c  ( column )

stub rem.display

{{
  ( Display block, with cursor )
  : pos  ( -cl)  rem:c @ rem:l @ ;
  : get  ( cl-a) (line) + ;
  : va   ( a-va) dup @ swap ;
  : c!   ( a-)   char: * swap ! ;
  : show ( va- ) dup c! (v) ! ;
  : display ( - ) pos get va show ;

  ( Bounds checking )
  : top ( - ) 0 rem:l ! ;
  : bot ( - ) 7 rem:l ! ;
  : beg ( - ) 0 rem:c ! ;
  : end ( - ) 63 rem:c ! ;
  : mid ( - ) 31 rem:c ! ;
  : 1st ( - ) 0 blk ! ;
  : bounds ( - )
    rem:c @ -1 =if end rem:l -- then
    rem:c @ 64 =if beg rem:l ++ then
    rem:l @ -1 =if top p then
    rem:l @  8 =if bot n then
    blk @ -1 =if 1st then
    blk @ #-blocks @ >if blk -- then
  ;

  : input ( - )
    repeat
      key
      dup 27 <> 0; drop
      dup  8 =if drop rem:c -- rem.display [ over 8 , , ] then
      dup 10 =if drop rem:l ++ else pos get ! rem:c ++ then
      rem.display
    again
  ;
  : ws    (  -  ) whitespace dup off @ later whitespace ! ;
  : match ( c-  ) s" $$_" dup push 2 + ! pop find if d->xt @ execute else drop then ;
  : edit? ( c-c ) dup 27 =if ws input drop then ;
  here ] dup 9 =if drop 27 then ; is (remap-keys)
  here ] bounds display ; is rem.display
  
---reveal---
  : edit  ( - ) repeat rem.display key edit? match again ;
  : $$i rem:l -- ;             ( i = up     )
  : $$j rem:c -- ;             ( j = left   )
  : $$k rem:l ++ ;             ( k = down   )
  : $$l rem:c ++ ;             ( l = right  )
  : $$m $$k beg ;              ( m = enter  )
  : $$I top ;                  ( I = top    )
  : $$J beg ;                  ( J = start  )
  : $$K bot ;                  ( K = bottom )
  : $$L end ;                  ( L = end    )
  : $$M mid ;                  ( M = middle )
  : $$p blk -- ;               ( p = previous block )
  : $$n blk ++ ;               ( n = next block )
  : $$z pop pop 2drop ;        ( z = exit REM )
  : $$e pop pop 2drop e ;      ( e = evaluate block )
}}


{{
  ( Character movement, current pos to end of line )
  : c:hom rem:l @ (line) ;
  : c:cur c:hom rem:c @ + ;
  : c:end c:hom 63 + ;
  : c:cur-hom    rem:c @   ;
  : c:cur-end 64 rem:c @ - ;
  : c:end-rec ( - )
    c:end @
    c:cur tib
    c:cur-end copy
    tib c:cur 1+
    c:cur-end 1- copy
    c:cur ! ;
  : c:end-sen ( - )
    c:cur @
    c:cur tib
    c:cur-end copy
    tib 1+ c:cur
    c:cur-end copy
    c:end ! ;

  ( Line swapping, with first or end line )
  : l:hom 0 (line) ;
  : l:cur rem:l @ (line) ;
  : l:end 7 (line) ;
  : l:up  l:cur 64 - ;
  : l:dn  l:cur 64 + ;
  : l:cur-up
    l:cur tib   64 copy
    l:up  l:cur 64 copy
    tib   l:up  64 copy rem:l -- ;
  : l:cur-dn
    l:cur tib   64 copy
    l:dn  l:cur 64 copy
    tib   l:dn  64 copy rem:l ++ ;
  : l:hom-swp
    l:cur tib   64 copy
    l:hom l:cur 64 copy
    tib   l:hom 64 copy ;
  : l:end-swp
    l:cur tib   64 copy
    l:end l:cur 64 copy
    tib   l:end 64 copy ;

  ( Hook for key sequences beginning with x. )
  : x-keys ( - )
    repeat
      key
      dup char: l =if c:end-rec then
      dup char: j =if c:end-sen then
      dup char: I =if l:cur-up  then
      dup char: K =if l:cur-dn  then
      dup char: i =if l:hom-swp then
      dup char: k =if l:end-swp then
      dup char: x =if drop   ;then
      rem.display
      drop
    again ;
---reveal---
  : $$x x-keys ;               ( x = x-keys )
}}
